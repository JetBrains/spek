apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/common/dependencies.gradle"


kotlin {
    jvm() {
        mavenPublication {
            artifactId = "spek-dsl-jvm"
        }
    }

    linuxX64("linux") {
        mavenPublication {
            artifactId = "spek-dsl-native-linux"
        }
    }

    macosX64("macOS") {
        mavenPublication {
            artifactId = "spek-dsl-native-macos"
        }
    }

    mingwX64("windows") {
        mavenPublication {
            artifactId = "spek-dsl-native-windows"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
    }
}

task stubJavadocJar(type: Jar) {
    classifier = 'javadoc'
}

ext.artifacts = ['metadata', 'jvm']

apply from: "$rootDir/gradle/common/publish.gradle"

publishing {
    kotlin.targets.all { target ->
        def targetPublication = publications.findByName(target.name)
        if (targetPublication != null) {
            targetPublication.artifact stubJavadocJar
        }
    }
}
