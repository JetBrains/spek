apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/common/dependencies.gradle"
apply from: "$rootDir/gradle/common/publish.gradle"

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm') {
            mavenPublication {
                artifactId = 'spek-runtime-jvm'
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api project(':spek-dsl')
            }
        }

        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation 'org.jetbrains.kotlin:kotlin-reflect'
                implementation 'io.github.classgraph:classgraph'
            }
        }
    }
}

task stubJavadocJar(type: Jar) {
    classifier = 'javadoc'
}

publishing {
    kotlin.targets.all { target ->
        def targetPublication = publications.findByName(target.name)
        if (targetPublication != null) {
            targetPublication.artifact stubJavadocJar
        }
    }
}