apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/common/dependencies.gradle"

kotlin {
    jvm() {
        mavenPublication {
            artifactId = 'spek-runtime-jvm'
        }
    }

    linuxX64("linux") {
        mavenPublication {
            artifactId = "spek-dsl-native-linux"
        }
    }

    macosX64("macOS") {
        mavenPublication {
            artifactId = "spek-dsl-native-macos"
        }
    }

    mingwX64("windows") {
        mavenPublication {
            artifactId = "spek-dsl-native-windows"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api project(':spek-dsl')
                implementation kotlin('stdlib')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common'
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation kotlin('reflect')
                implementation 'io.github.classgraph:classgraph'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
            }
        }

        nativeMain {
            dependsOn commonMain
        }

        linuxMain {
            dependsOn nativeMain
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.1.1'
            }
        }

        macOSMain {
            dependsOn nativeMain
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.1.1'
            }
        }

        windowsMain {
            dependsOn nativeMain
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.1.1'
            }
        }

        nativeTest {
            dependsOn commonTest

            dependencies {
                implementation kotlin('test')
            }
        }

        linuxTest {
            dependsOn nativeTest
        }

        macOSTest {
            dependsOn nativeTest
        }

        windowsTest {
            dependsOn nativeTest
        }
    }
}

ext.artifacts = ['metadata', 'jvm']

apply from: "$rootDir/gradle/common/publish.gradle"

task stubJavadocJar(type: Jar) {
    classifier = 'javadoc'
}

publishing {
    kotlin.targets.all { target ->
        def targetPublication = publications.findByName(target.name)
        if (targetPublication != null) {
            targetPublication.artifact stubJavadocJar
        }
    }
}
