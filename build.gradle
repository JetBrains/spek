import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.16'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.4.2"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.3"
    }
}

plugins {
    id 'org.ajoberstar.reckon' version '0.8.0'
}

reckon {
    scopeFromProp()
    stageFromProp('alpha', 'rc', 'final')
}

afterEvaluate {
    def v = version
    allprojects {
        version = v
    }
}


allprojects {
    // Previously this caused a circular dependency (for some reason), it now works.
    group = 'org.spekframework.spek2'

    repositories {
        jcenter()
    }

    if (Files.exists(Paths.get("$rootDir/local.properties"))) {
        def localProperties = new Properties()
        localProperties.load(new FileInputStream("$rootDir/local.properties"))
        localProperties.each { prop -> project.ext.set(prop.key, prop.value) }
    }

    def releaseMode = false
    if ("$version".matches("^\\d+\\.\\d+\\.\\d+(-rc\\.\\d+)?")) {
        releaseMode = true
    }
    project.ext.releaseMode = releaseMode


    apply plugin: 'idea'
}
