apply from: "$rootDir/gradle/common/dependencies.gradle"
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin-platform-jvm'

def buildMatrix = [
    AS32: [prefix: 'Studio3.2', since: '181.5540', until: '181.5540.*', deps: ['org.jetbrains.kotlin:1.3.0-release-Studio3.2-1']],
    AS33: [prefix: 'Studio3.3', since: '182.3911', until: '182.3911.*', deps: ['org.jetbrains.kotlin:1.3.0-release-Studio3.3-1']],
]

String sdkVersion = "AS32"

if (hasProperty('as.version')) {
    sdkVersion = getProperty('as.version')
}

def settings = buildMatrix[sdkVersion]

intellij {
    pluginName = "Spek Framework"
    localPath = "$localAndroidStudio"
    plugins = ['gradle'] + settings['deps']
    alternativeIdePath = "$localAndroidStudio"
}

dependencies {
    compile project(':spek-ide-plugin:intellij-base-jvm')
    compileOnly fileTree(dir: "$localAndroidStudio/plugins/android/lib", include: ['*.jar'])
}

buildPlugin {
    baseName = ''
    version = "${project.version}-Studio3.2"
}

patchPluginXml {
    version = "${project.version}-${settings['prefix']}"
    sinceBuild = settings['since']
    untilBuild = settings['until']
}
