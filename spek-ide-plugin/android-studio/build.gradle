apply from: "$rootDir/gradle/common/dependencies.gradle"
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin-platform-jvm'

def buildMatrix = [
    AS32: [sdk: '181.5540.23', prefix: 'Studio3.2', since: '181.5540', until: '181.5540.*', deps: ['org.jetbrains.kotlin:1.3.0-release-Studio3.2-1']],
    AS33: [sdk: '182.5107.41', prefix: 'Studio3.3', since: '182.5107', until: '182.5107.*', deps: ['org.jetbrains.kotlin:1.3.0-release-Studio3.3-1']],
]

String sdkVersion = "AS33"

if (hasProperty('as.version')) {
    sdkVersion = getProperty('as.version')
}

def settings = buildMatrix[sdkVersion]

intellij {
    pluginName = "Spek Framework"
    version = settings['sdk']
    plugins = ['gradle', 'android'] + settings['deps']
    if (hasProperty("local${sdkVersion}Path")) {
        alternativeIdePath = getProperty("local${sdkVersion}Path")
    }
}

dependencies {
    compile project(':spek-ide-plugin:intellij-base-jvm')
}

buildPlugin {
    baseName = ''
    version = "${project.version}-${settings['prefix']}"
}

patchPluginXml {
    version = "${project.version}-${settings['prefix']}"
    sinceBuild = settings['since']
    untilBuild = settings['until']
}
