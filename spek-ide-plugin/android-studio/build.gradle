apply from: "$rootDir/gradle/common/dependencies.gradle"
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin-platform-jvm'
apply from: "$rootDir/gradle/common/publish-plugin.gradle"

def buildMatrix = [
    AS33: [sdk: '182.5107.16', extraSource: 'AS33', prefix: 'Studio3.3', since: '182.5107', until: '182.5107.*', deps: ['org.jetbrains.kotlin:1.3.30-release-Studio3.3-1']],
    AS34: [sdk: '183.6156.11', extraSource: 'AS34', prefix: 'Studio3.4', since: '183.6156', until: '183.6156.*', deps: ['org.jetbrains.kotlin:1.3.30-release-Studio3.4-1']],
    AS35: [sdk: '191.7141.44', extraSource: 'AS34', prefix: 'Studio3.5', since: '191.7141', until: '191.7141.*', deps: ['org.jetbrains.kotlin:1.3.30-release-Studio3.5-1']],
]

String sdkVersion = "AS34"

if (hasProperty('as.version')) {
    sdkVersion = getProperty('as.version')
}

def settings = buildMatrix[sdkVersion]

sourceSets {
    main {
        kotlin {
            srcDirs += "src/${settings['extraSource']}/kotlin"
        }
    }
}


intellij {
    pluginName = "Spek Framework"
    version = settings['sdk']
    plugins = ['gradle', 'android'] + settings['deps']
    if (project.hasProperty("local${sdkVersion}Path")) {
        localPath = getProperty("local${sdkVersion}Path")
    }
}

dependencies {
    compile project(':spek-ide-plugin:intellij-base-jvm')
}

buildPlugin {
    baseName = ''
    version = "${project.version}-${settings['prefix']}"
}

patchPluginXml {
    version = "${project.version}-${settings['prefix']}"
    sinceBuild = settings['since']
    untilBuild = settings['until']
}
