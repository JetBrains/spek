apply from: "$rootDir/gradle/common/dependencies.gradle"
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin-platform-jvm'
apply from: "$rootDir/gradle/common/publish-plugin.gradle"

def buildMatrix = [
        IJ182: [sdk: '2018.2.5', prefix: 'IJ2018.2', extraSource: 'IJ182', since: '182.1', until: '182.*', deps: ['org.jetbrains.kotlin:1.3.0-release-IJ2018.2-1']],
        IJ183: [sdk: '183.4284.36', prefix: 'IJ2018.3', extraSource: 'IJ183', since: '183.1', until: '183.*', deps: ['org.jetbrains.kotlin:1.3.0-release-IJ2018.3-1']],
        // todo: update deps when kotlin plugin for IJ191 is available
        IJ191: [sdk: '191.4212.41', prefix: 'IJ2019.1', extraSource: 'IJ183', since: '191.1', until: '191.*', deps: ['org.jetbrains.kotlin:1.3.20-release-IJ2018.3-1']],
]

String sdkVersion = "IJ183"

if (hasProperty('ij.version')) {
    sdkVersion = getProperty('ij.version')
}

def settings = buildMatrix[sdkVersion]

intellij {
    pluginName = 'Spek Framework'
    plugins = settings['deps']
    version = settings['sdk']
}

sourceSets {
    main {
        kotlin {
            srcDirs += "src/${settings['extraSource']}/kotlin"
        }
    }
}

dependencies {
    compile project(':spek-ide-plugin:intellij-base-jvm')
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

buildPlugin {
    baseName = ''
    version = "${project.version}-${settings['prefix']}"
}

patchPluginXml {
    version = "${project.version}-${settings['prefix']}"
    sinceBuild = settings['since']
    untilBuild = settings['until']
}
