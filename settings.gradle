// Temporary worakround for https://github.com/Kotlin/dokka/issues/146
// Must put jcenter() first to resolve the correct artifacts for Dokka
pluginManagement {
    repositories {
        jcenter()
        gradlePluginPortal()
    }
    resolutionStrategy {
        eachPlugin {
            if ( requested.id.id == 'org.jetbrains.kotlin.multiplatform' || requested.id.id == 'kotlin-jvm') {
                useModule( "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion" )
            }
        }
    }
}

enableFeaturePreview('STABLE_PUBLISHING')

rootProject.name = 'spek'

gradle.ext.includedBuilds = [
    'spek-gradle-plugin'
]

include 'spek-dsl'
include 'spek-kotlin-compiler-plugin'
include 'spek-runtime'
include 'spek-runner:junit5'
include 'integration-test'

gradle.ext.includedBuilds.forEach { includeBuild it }

if (!hasProperty("excludeIdePlugins")) {
    include 'spek-ide-plugin:interop-jvm'
    include 'spek-ide-plugin:intellij-base'
    include 'spek-ide-plugin:intellij-base-jvm'
    include 'spek-ide-plugin:intellij-idea'
    include 'spek-ide-plugin:android-studio'
}
