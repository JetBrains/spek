apply plugin: 'kotlin-multiplatform'
apply from: "$rootDir/gradle/common/dependencies.gradle"

kotlin {
    jvm()

    // Setting isTestCompilation = false disables kotlin-test.
    linuxX64("linux") {
        compilations.test.isTestCompilation = false
    }

    macosX64("macOS") {
        compilations.test.isTestCompilation = false
    }

    mingwX64("windows") {
        compilations.test.isTestCompilation = false
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }

        commonTest {
            dependencies {
                implementation project(':spek-dsl')
                implementation kotlin('test')
                implementation project(':spek-runtime')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }

        jvmTest {
            dependencies {
                runtimeOnly kotlin('reflect')
                runtimeOnly project(':spek-runner:junit5')
            }
        }

        nativeMain {
            dependsOn commonMain
        }

        nativeTest {
            dependsOn commonTest

            dependencies {
                implementation project(':spek-runtime')
            }
        }

        linuxMain {
            dependsOn nativeMain
        }

        macOSMain {
            dependsOn nativeMain
        }

        windowsMain {
            dependsOn nativeMain
        }

        linuxTest {
            dependsOn nativeTest
        }

        macOSTest {
            dependsOn nativeTest
        }

        windowsTest {
            dependsOn nativeTest
        }
    }
}

jvmTest {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}
