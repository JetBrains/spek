buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        // keep in-sync with gradle.properties kotlinVersion
        // weirdly, this fails on windows.
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.30"
    }
}

plugins {
    id 'org.ajoberstar.reckon' version '0.8.0' apply false
}

repositories {
    mavenCentral()
}

apply from: "../gradle/common/versioning.gradle"
apply from: "../gradle/common/dependencies.gradle"
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin-platform-jvm'
apply plugin: 'kotlin-kapt'

gradlePlugin {
    plugins {
        create("spekPlugin") {
            id = "${project.group}.gradle-plugin"
            implementationClass = "org.spekframework.spek2.gradleplugin.SpekPlugin"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlinVersion"

    implementation "com.google.auto.service:auto-service:1.0-rc4"
    kapt "com.google.auto.service:auto-service:1.0-rc4"
}

kapt {
    includeCompileClasspath = false
}

jar {
    manifest {
        attributes('Implementation-Version': version)
    }
}
